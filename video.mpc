\\
\\                    Video.mpc
\\
\\ Assigns MED_VM_Interface.dll Connection constants (defined in
\\ VM_Iface.hed)  to local array "Q".
\\ Attempts connection with VideoMonitor over LAN (domain or workgroup). 
\\ If it does not connect, it retries every 1/10 second.
\\ If it connects with Video Monitor, a video is recorded for 10 seconds, 
\\ then the connection is terminated.

\\ Q Array holds MED-VM interface connections status states
\\ Q(0) = Unconnected 
\\ Q(1) = Good Connection 
\\ Q(2) = FAILED Connection

DIM Q = 2         \\ 2 + 1 = 3 element array

S.S.1,
S1,
  1":     ~Q[0] := UNCONNECTED;
           Q[1] := CONNECT_OK;
           Q[2] := CONNECT_FAIL;
           ConnectToVMEx(MG, BOX, '.', 'Cam');~;
          SHOW 3,Unconnected=,Q(0), 4,Connected=,Q(1), 5,Failure=,Q(2) ---> S2

S2,
  0.1":   ~X := GetIfaceStatus();~;
          SHOW 2,Status:,X;
          \\ X is Interface Status value from MED_VM_Interface.dll

          IF (X = Q(0)) [@UNCON, @OTHER]
             @UNCON: SHOW 1, Unconnected,0 ---> S2
             @OTHER: IF (X = Q(1)) [@CONNECTED, @OTHER2]
                @CONNECTED: SHOW 1, Connected OK,0 ---> S3
                @OTHER2: IF (X = Q(2)) [@FAIL, @OTHER3]
                   @FAIL:   SHOW 1, Connection Failed,0 ---> S32
                   @OTHER3: SHOW 1, Unknown State,0  ---> S32

S3, 
  1":     ~WriteEventVM('.', 'Cam', StartSavingEvent, 'StartEvent');~ ---> S4

S4,
  10":    ~WriteEventVM('.', 'Cam', StopSavingEvent, 'StopEvent');~ ---> S5

S5,
  0.01":  ~DisconnectVM('.', 'Cam');~ ---> S32

S32,
  0.01":  ---> STOPKILL